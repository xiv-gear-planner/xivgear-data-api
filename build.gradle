plugins {
	id 'groovy'
	id 'io.micronaut.application' version '4.4.2'
	id 'io.micronaut.openapi' version '4.4.2'
}

group = 'app.xivgear'
version = '1.0-SNAPSHOT'

repositories {
	mavenLocal()
	mavenCentral()
}

micronaut {
	runtime "netty"
	testRuntime "junit5"
	dockerfile {
		args "-XX:+UseG1GC"
		args "-XX:MinHeapFreeRatio=15"
		args "-XX:MaxHeapFreeRatio=30"
		args "-XX:MaxRAMPercentage=60"
		// TODO measure performance impact
		args "-XX:+UseStringDeduplication"
	}
}

tasks.named('dockerfile') {
	baseImage = 'eclipse-temurin:21'
}

dependencies {
	implementation 'org.apache.groovy:groovy:4.0.22'
	implementation group: 'app.xivgear', name: 'xivapi-java', version: '0.1.13'

	testImplementation platform('org.junit:junit-bom:5.11.0')
	testImplementation 'org.junit.jupiter:junit-jupiter'

	implementation "io.micronaut:micronaut-runtime"
	implementation "jakarta.annotation:jakarta.annotation-api"
	implementation "io.micronaut.openapi:micronaut-openapi:6.16.0"
	implementation "io.swagger.core.v3:swagger-core:2.2.31"
	implementation "io.swagger.core.v3:swagger-annotations:2.2.31"
//	implementation "io.swagger.core.v3:swagger-ui"
	implementation "io.micronaut.validation:micronaut-validation"
	annotationProcessor "io.micronaut.openapi:micronaut-openapi:6.16.0"

	implementation 'org.slf4j:slf4j-api:2.0.12'
	runtimeOnly 'ch.qos.logback:logback-classic:1.5.7'

	// Jackson JSON support
	implementation "io.micronaut:micronaut-jackson-databind"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"

	// For dependency injection
	implementation "io.micronaut:micronaut-inject"
	// https://mvnrepository.com/artifact/jakarta.inject/jakarta.inject-api
	implementation group: 'jakarta.inject', name: 'jakarta.inject-api', version: '2.0.1'


	// Test dependencies
	testImplementation("io.micronaut.test:micronaut-test-junit5")
	testImplementation("org.junit.jupiter:junit-jupiter-api")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")

	// object storage
	// core
	implementation("io.micronaut.objectstorage:micronaut-object-storage-core")
	// Oracle Cloud
	implementation("io.micronaut.objectstorage:micronaut-object-storage-oracle-cloud") {
//		exclude group: "io.micronaut.serde", module: "micronaut-serde-api"
	}
	// S3 compatible (for Ceph RGW)
	implementation("io.micronaut.objectstorage:micronaut-object-storage-aws")
	// Local for testing
	implementation("io.micronaut.objectstorage:micronaut-object-storage-local")

	testImplementation "io.micronaut.test:micronaut-test-junit5:1.1.5"
	testImplementation "io.micronaut:micronaut-inject-groovy"
	testImplementation "io.micronaut:micronaut-http-client"

	implementation project(":common-libs:logging")
}

test {
	systemProperties["junit.jupiter.execution.parallel.enabled"] = true
	systemProperties["junit.jupiter.execution.parallel.mode.default"] = "concurrent"
	useJUnitPlatform()
	systemProperties['xivapi.baseUri'] = System.getProperty("xivapi.baseUri") ?: 'https://bm.xivgear.app/api/1'

}

application {
	mainClass.set "gg.xp.xivgear.dataapi.Main"
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(GroovyCompile).configureEach {
	groovyOptions.configurationScript = file(project.rootDir.toPath().resolve("groovyconfig.groovy").toFile())
	groovyOptions.fork = true
	groovyOptions.forkOptions.jvmArgs << '-Dmicronaut.openapi.views.spec=swagger-ui.enabled=true'
	groovyOptions.forkOptions.jvmArgs << '-Dmicronaut.jsonschema.strictMode=true'
//	groovyOptions.forkOptions.jvmArgs << '-Dmicronaut.openapi.json.format=true'
}